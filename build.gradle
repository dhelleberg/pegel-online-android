import groovy.swing.SwingBuilder
repositories {
    google()
    jcenter()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        //classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.1.1'
    }
}
apply plugin: 'com.android.application'
//apply plugin: 'com.getkeepsafe.dexcount'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    androidTestCompile fileTree(dir: 'libs-test', include: '*.jar')
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.3.1'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'

    compile 'com.android.support:appcompat-v7:28.0.0'
    compile 'com.android.support:design:28.0.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.android.support:support-v4:28.0.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'


    compile(name:'inovex-acra-release-4.8.2', ext:'aar')

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}


gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':assembleRelease')) {

        def pass = ''
        if(System.console() == null) {
            new SwingBuilder().edt {
                dialog(modal: true, // Otherwise the build will continue running before you closed the dialog
                        title: 'Enter password', // Dialog title
                        alwaysOnTop: true, // pretty much what the name says
                        resizable: false, // Don't allow the user to resize the dialog
                        locationRelativeTo: null, // Place dialog in center of the screen
                        pack: true, // We need to pack the dialog (so it will take the size of it's children)
                        show: true // Let's show it
                ) {
                    vbox { // Put everything below each other
                        label(text: "Please enter key passphrase:")
                        input = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            pass = input.password; // Set pass variable to value of input field
                            dispose(); // Close dialog
                        })
                    } // vbox end
                } // dialog end
            } // edt end
        } else {
            passwd = System.console().readPassword("\nPlease enter key passphrase: ")
            pass = new String(passwd)
        }

        android.signingConfigs.release.storePassword = new String(pass)
        android.signingConfigs.release.keyPassword = new String(pass)
    }
}

android {
    signingConfigs {
        release {
            keyAlias 'cirrus25'
            storeFile file('../android-keystore')
            storePassword ''
            keyPassword ''
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        testApplicationId "org.cirrus.mobi.pegel.tests"
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        testApplicationId 'org.cirrus.mobi.pegel.tests'
        minSdkVersion 14
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']

            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        androidTest {
            java.srcDirs = ['tests/src']
            resources.srcDirs = ['tests/src']

        }
        test {
            java {
                srcDir 'java-tests'
            }
        }

    }
    productFlavors {
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'

        }
    }
}

